# List all of the available commands.
default:
  just --list

update:
	nix flake update

# Get the hostname for a node
host node:
	#!/usr/bin/env bash
	DOMAIN=$(tofu output -raw domain)
	echo "{{node}}.$DOMAIN"

# Deploy moq-relay and moq-cert to all nodes
[parallel]
deploy-all: (deploy "usc") (deploy "euc") (deploy "sea")

# List the available nodes.
nodes:
	@echo "usc: Dallas, TX"
	@echo "euc: Frankfurt, Germany"
	@echo "sea: Singapore"

# Deploy moq-relay and moq-cert to a specific node
deploy node:
	#!/usr/bin/env bash
	set -euo pipefail
	HOST=$(just host {{node}})
	echo "Deploying to $HOST..."

	echo "  Copying flake files to /var/lib/moq"
	rsync -az flake.nix flake.lock root@$HOST:/var/lib/moq/

	echo "  Copying secrets to /var/lib/moq"
	rsync -az secrets/ root@$HOST:/var/lib/moq/
	ssh root@$HOST "chmod -R 755 /var/lib/moq && chmod 600 /var/lib/moq/*.jwk /var/lib/moq/*.jwt"

	echo "  Copying systemd units to /etc/systemd/system/..."
	rsync -az relay/ root@$HOST:/etc/systemd/system/

	echo "  Building and caching packages..."
	ssh root@$HOST "cd /var/lib/moq && nix build .#moq-relay -o relay && nix build .#certbot -o cert"

	echo "  Enabling and starting services..."
	ssh root@$HOST "\
		systemctl daemon-reload && \
		systemctl enable moq-cert.service && \
		systemctl enable certbot-renew.timer && \
		systemctl enable vacuum.timer && \
		systemctl enable moq-relay.service && \
		systemctl start certbot-renew.timer && \
		systemctl start vacuum.timer && \
		systemctl start moq-cert.service && \
		systemctl restart moq-relay.service"

# Check status of a node
status node:
	#!/usr/bin/env bash
	HOST=$(just host {{node}})
	echo "=== $HOST ==="
	ssh root@$HOST "systemctl status moq-relay --no-pager"

status-all: (status "usc") (status "euc") (status "sea")

ssh node:
	#!/usr/bin/env bash
	ssh root@$(just host {{node}})

# View logs from a specific node
logs node:
	#!/usr/bin/env bash
	ssh root@$(just host {{node}}) "journalctl -u moq-relay -f"

# Deploy publisher
deploy-pub:
	#!/usr/bin/env bash
	set -euo pipefail
	HOST=$(just host pub)
	echo "Deploying to $HOST..."

	echo "  Copying flake files to /var/lib/moq"
	rsync -az flake.nix flake.lock root@$HOST:/var/lib/moq/

	echo "  Copying secrets to /var/lib/moq"
	rsync -az secrets/ root@$HOST:/var/lib/moq/
	ssh root@$HOST "chmod -R 755 /var/lib/moq && chmod 600 /var/lib/moq/*.jwt"

	echo "  Copying systemd units to /etc/systemd/system/..."
	rsync -az pub/ root@$HOST:/etc/systemd/system/

	echo "  Building and caching packages..."
	ssh root@$HOST "cd /var/lib/moq && nix build .#ffmpeg -o result && nix build .#hang-cli -o result-hang"

	echo "  Enabling and starting services..."
	ssh root@$HOST "\
		systemctl daemon-reload && \
		systemctl enable hang-bbb-prepare.service && \
		systemctl enable hang-bbb.service && \
		systemctl enable vacuum.timer && \
		systemctl start hang-bbb-prepare.service && \
		systemctl start vacuum.timer && \
		systemctl restart hang-bbb.service"

# Check status of publisher
status-pub:
	#!/usr/bin/env bash
	HOST=$(just host pub)
	echo "=== $HOST ==="
	ssh root@$HOST "systemctl status hang-bbb --no-pager"

# SSH into publisher
ssh-pub:
	#!/usr/bin/env bash
	HOST=$(just host pub)
	ssh root@$HOST

# View logs from publisher
logs-pub:
	#!/usr/bin/env bash
	HOST=$(just host pub)
	ssh root@$HOST "journalctl -u hang-bbb -f"

# Run health checks against all relay nodes
health:
	./health.sh

# Run health checks with webhook notification on failure
health-notify webhook:
	./health.sh {{webhook}}

# Check tofu formatting
check:
	tofu fmt -check -recursive

# Fix tofu formatting
fix:
	tofu fmt -recursive
